unit sample

use {
    lang.Sys
}

share InteractiveDemo {
    
    // Supporting both else-if and elif
    local calculate(a: int, b: int, op: text)
    :: formula: int|float, operation: text {  
        if op == "+" {
           // Separate return assignments
           ~> formula: a + b
           ~> operation: "addition"
         }
         else if op == "-"
           // One line return assignments
           ~> formula: a - b, operation: "subtraction"
         else if op == "*"
           // implicit one line return assignments
           ~> a * b, "multiplication"
         else if op == "/"
           // mixed one line return assignments
           ~> formula: a / b, "division"
         else
           ~> 0, "unknown"
    }

    // Method with single expression
    local add(a: int, b: int) ~> a + b

    local haha(a: int) :: x: int, y: int {
    	// Without '~> value' this will make a compile error
    }

    
    // Array demonstration
    local demonstrateArrays() {
        outln("=== Array Demo ===")

        num1: [] = [1, 2, 3]
        
        outln("Trying num1: [] = " + num1)
        
        num2: [int] = [2, 3, 4, 5, 6]
        
        outln("Trying num2: [int] = " + num2)
        
                // With implicitness
        num3 := [10, 20, 30, 40, 50]
        outln("Trying: num3 := " + num3)
        
        outln("First element: " + num3[0])
        outln("Last element: " + num3[4])

        num3[2] = 99
        outln("After modification: " + num3)
    }

    // in() demonstration with ENHANCED VALIDATION
    local getUserInfo() {
        outln("=== User Registration ===")
    
        outln("Enter your name:")
        name := in()
    
        outln("Enter your age:")
        age :=  in(int)
    
        outln("Enter your height (meters):")
        height := in(float)
    
        outln("Are you a student? (true/false)")
        isStudent := in(bool)
    
        // NEW: Showcasing 'all' for comprehensive validation
        if all[
            name != "",
            age >= 0, 
            age <= 120,
            height > 0.0,
            height < 3.0
       ] {
            outln("")
            outln("Registration Complete!")
            outln("Name: " + name)
            outln("Age: " + age)
            outln("Height: " + height + "m")
            outln("Student: " + isStudent)
        
            // NEW: Showcasing 'any' for role-based greeting
            if any[isStudent, age < 25] {
                outln("Welcome young user " + name + "!")
            } else {
                outln("Welcome " + name + "!")
            }
         } else {
             outln("")
             outln(" Registration Failed: Invalid data provided!")
         }
    }

    // Calculator with user in() + VALIDATION SHOWCASE
    local interactiveCalculator() {
        outln("=== Interactive Calculator ===")
    
        outln("Enter first number:")
        num1 :=  in(int)
    
        outln("Enter second number:")
        num2 :=  in(int)
    
        outln("Enter operation (+, -, *, /):")
        op := in()
    
        //  NEW: Showcasing conditional chains for validation!
        if op == any["+", "-", "*", "/"] {
            result, operation := [formula, operation]:calculate(num1, num2, op)
        
            outln("")
            outln("Calculation Result:")
            outln(num1 + " " + op + " " + num2 + " = " + result)
            outln("Operation: " + operation)
        } else {
            outln("")
            outln("Error: '" + op + "' is not a valid operation!")
            outln("Please use one of: +, -, *, /")
        }
    }
    
    // Loop demonstration with in()
    share numberSeries() {
        outln("=== Number Series Generator ===")
        
        outln("Enter start number:")
        start :=  in(int)
        
        outln("Enter end number:")
        end :=  in(int)
        
        outln("Number series from " + start + " to " + end + ":")
        
        outln("")
        outln("=== DEFAULT STEPS (No 'by' clause) ===")
        outln("Counting up naturally:")
        for i in 1 to 5 {
            outln("Default step: " + i)  // 1, 2, 3, 4, 5
        }
        
        outln("Counting down naturally:")
        for i in 5 to 1 {
            outln("Default step: " + i)  // 5, 4, 3, 2, 1
        }
        
        outln("User range - smart default:")
        for i in start to end {
            outln("Smart default: " + i)  // auto +1 or -1 based on start/end
        }
        
        outln("")
        outln("=== BASIC STEPS ===")
        outln("Enter step size:")
        steps := in(int)
        
        for i by steps in start to end {
            outln("Step by " + steps + ": " + i)
        }
        
        // Operator prefixes
        for i by +1 in start to end {
            outln("Step by +1: " + i)
        }
        
        // Counting down with various methods
        for i by -1 in 10 to 1 {
            outln("Countdown by -1: " + i)
        }
        
        outln("=== MULTIPLICATIVE STEPS ===")
        
        // Multiplicative patterns
        
        for i by *2 in 1 to 32 {
            outln("Doubling: " + i)
        }
        
        exponent := 1
        powerValue := 2
        for i in 1 to 6 {
           outln("Power compound: 2^" + exponent + " = " + powerValue)
           exponent = exponent + 1
           powerValue = powerValue * 2
        }
        
        for i by *+2 in start to end {
            outln("Step by *+2: " + i)
        }
        
        outln("")
        outln("=== DIVISION STEPS ===")
        
        // Division patterns
        for i by /2 in 64 to 2 {
            outln("Halving: " + i)
        }
        
        for i by /+2 in 32 to 1 {
            outln("Step by /+2: " + i)
        }
        
        outln("")
        outln("=== PRACTICAL PATTERNS ===")
        outln("Powers of 2:")
        
        exponent = 1
        for value by *2 in 2 to 64 {  // Start from 2 (2^1)
           outln("2^" + exponent + " = " + value)
           exponent = exponent + 1
        }
        
        outln("Countdown sequences:")
        // Natural countdown
        for count in 10 to 1 {
            outln("Natural countdown: " + count)
        }
        
        // Explicit countdown
        for count by -1 in 10 to 1 {
            outln("Explicit countdown: " + count)
        }
        
        grow := 1
        for i in 1 to 5 {  // Limit iterations
            outln("Growing: " + grow)
        grow = grow * 2 + 1
        }
        
        outln("Mixed operations:")
        for mixed in 1 to 20 {
            outln("Mixed default: " + mixed)
        }
        
        for mixed by +2 in 1 to 20 {
            outln("Mixed +2: " + mixed)
        }
        
        for mixed by -2 in 20 to 2 {
            outln("Mixed -2: " + mixed)
        }
    }
    
    // Additional demo for edge cases
    share edgeCaseLoops() {
        outln("=== Edge Case Loops ===")
        
        // Single element ranges
        outln("Single element:")
        for i in 5 to 5 {
            outln("Single: " + i)  // Should outln: 5
        }
        
        // Zero-step ranges
        outln("Zero range up:")
        for i in 0 to 0 {
            outln("Zero up: " + i)  // Should outln: 0
        }
        
        outln("Zero range down:")
        for i in 0 to 0 {
            outln("Zero down: " + i)  // Should outln: 0
        }
        
        // Negative numbers
        outln("Negative ranges:")
        for i in -5 to -1 {
            outln("Negative up: " + i) // -5, -4, -3, -2, -1
        }
        
        for i in -1 to -5 {
            outln("Negative down: " + i) // -1, -2, -3, -4, -5
        }
        
        // Large steps
        outln("Large steps:")
        for i by 10 in 0 to 50 {
            outln("Large step: " + i ) // 0, 10, 20, 30, 40, 50
        }
    }
    
    // Main method - program entry point
    share main() {
        outln("=== CODERIVE INTERACTIVE DEMO ===")
        outln("")
        
        // Demonstrate arrays
        demonstrateArrays()
        outln("")
        
        // Get user information
        getUserInfo() 
        outln("")
        
        // Calculator demo
        interactiveCalculator()
        outln("")
        
        // Number series demo
        numberSeries()
        outln("")
        
        // Edge cases
        edgeCaseLoops()
        outln("")
        
        outln("=== DEMO COMPLETE ===")
        outln("Thank you for using Coderive!")
    }
}